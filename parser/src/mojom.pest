line_comment = { "//" ~ (!NEWLINE ~ ANY)* }
block_comment = { "/*" ~ (!"*/" ~ ANY)* ~ "*/" }
COMMENT = _{ line_comment | block_comment }

WHITESPACE = _{ " " | "\t" | NEWLINE }

char = { ASCII_ALPHA | "_" }

name = @{ char ~ (char | ASCII_DIGIT)* }

attribute_section = {
  "[" ~ "]" |
  "[" ~ attribute ~ ("," ~ attribute)* ~ "]"
}
attribute = { name ~ "=" ~ name | name ~ "=" ~ literal | name }

struct_stmt = { attribute_section? ~ "struct" ~ name ~ struct_body? ~ ";" }
struct_body = { "{" ~ struct_item* ~ "}" }
struct_item = {
  const_stmt |
  enum_stmt |
  struct_field
}
struct_field = { attribute_section? ~ type_spec ~ name ~ ordinal_value? ~ default? ~ ";" }
default = { "=" ~ constant }

interface = {
  attribute_section? ~ "interface" ~ name ~ "{" ~ interface_body* ~ "}" ~ ";"
}
interface_body = {
  const_stmt | enum_stmt | method_stmt
}

identifier = @{ name ~ "." ~ identifier | name }

type_spec = {
  type_name ~ "?" |
  type_name
}
type_name = { fixed_array | array | map | interface_request | basic_type_name }
basic_type_name = {
  numeric_type | handle_type | "associated" ~ identifier | identifier
}
numeric_type = {
  "bool" | "int8" | "uint8" | "int16" | "uint16" | "int32" | "uint32" |
  "int64" | "uint64" | "float" | "double"
}
handle_type = { "handle" ~ ("<" ~ specific_handle_type ~ ">")? }
specific_handle_type = {
  "message_pipe" | "shared_buffer" | "data_pipe_consumer" |
  "data_pipe_producer"
}
array = { "array" ~ "<" ~ type_spec ~ ">" }
fixed_array = { "array" ~ "<" ~ type_spec ~ "," ~ int_const_dec ~ ">" }
map = { "map" ~ "<" ~ identifier ~ "," ~ type_spec ~ ">" }
interface_request = {
  "associated" ~ identifier ~ "&" |
  identifier ~ "&"
}

const_stmt = {
  attribute_section? ~ "const" ~ type_spec ~ name ~ "=" ~ constant ~ ";"
}
constant = { (literal | identifier) }

enum_stmt = {
  attribute_section? ~ "enum" ~ name ~ "{"
    ~ enum_value ~ ("," ~ enum_value)* ~ ","? ~
  "}" ~ ";"
}
enum_value = {
  attribute_section? ~ name ~ "=" ~ integer |
  attribute_section? ~ name ~ "=" ~ identifier |
  attribute_section? ~ name
}

method_stmt = {
  attribute_section? ~
  name ~
  ordinal_value? ~
  parameter_list ~
  response? ~
  ";"
}
parameter_list = {
  "(" ~ ")" |
  "(" ~ parameter ~ ("," ~ parameter)* ~ ")"
}
parameter = { attribute_section? ~ type_spec ~ name ~ ordinal_value? }
response = { "=>" ~ parameter_list }

// TODO: Support float
literal = { integer | "true" | "false" | "default" | string_literal }

integer = @{ int_const | "+" ~ int_const | "-" ~ int_const }
int_const = @{ int_const_hex | int_const_dec }
int_const_dec = @{ "0" | (ASCII_NONZERO_DIGIT ~ ASCII_DIGIT*) }
int_const_hex = @{ "0" ~ ("x" | "X") ~ ASCII_HEX_DIGIT+ }
ordinal_value = @{ "@" ~ int_const_dec }

string_literal = ${ "\"" ~ string_inner ~ "\"" }
string_inner = @{ string_char* }
string_char = {
  !("\"" | "\\") ~ ANY
  | "\\" ~ ("\"" | "\\" | "/" | "b" | "f" | "n" | "r" | "t")
}

mojom_file = {
  SOI ~ (interface)* ~ EOI
}
